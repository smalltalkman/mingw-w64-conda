# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=pycosat
pkgbase=mingw-w64-python-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python-${_realname}"
pkgver=0.6.3
pkgrel=1
pkgdesc="Python bindings to picosat (a SAT solver) (mingw-w64)"
arch=('any')
url="https://github.com/conda/pycosat"
license=('MIT')
mingw_arch=('mingw32' 'mingw64')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("patch"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cc")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-nose")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/conda/pycosat/archive/${pkgver}.tar.gz
        0001-define-macro-NGETRUSAGE.patch)
sha256sums=('abcaf3d6afb332b0d8363590d0aaa81f00f71142900fbae2608f7d595d5e4492'
            '05d064d044038c209d2a303b53649b1f048e813bfceda1336215b26d611800dc')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  rm -rf "${srcdir}/${_realname}-${pkgver}.orig"
  cp -rp "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}.orig"

  cd "${srcdir}/${_realname}-${pkgver}"
  apply_patch_with_msg \
    0001-define-macro-NGETRUSAGE.patch

  rm -rf "${srcdir}/${_realname}-${pkgver}.ptch"
  cp -rp "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}.ptch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python setup.py --quiet build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  local python_platform=$(python -c 'import sysconfig; print(sysconfig.get_platform())')
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  local python_path=$(cygpath -m "$PWD/build/lib.${python_platform}-${python_version}")
  PYTHONPATH="$python_path;$PYTHONPATH" \
  ${MINGW_PREFIX}/bin/nosetests || warning "Tests failed"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
