# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=pycosat
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.6.3
pkgrel=1
pkgdesc="Python bindings to picosat (a SAT solver) (mingw-w64)"
arch=('any')
url="https://github.com/ContinuumIO/pycosat"
license=('MIT')
_archive=${_realname}-${pkgver}
source=(${_archive}.tar.gz::https://github.com/ContinuumIO/pycosat/archive/${pkgver}.tar.gz
        0001-define-macro-NGETRUSAGE.patch)
sha256sums=('ABCAF3D6AFB332B0D8363590D0AAA81F00F71142900FBAE2608F7D595D5E4492'
            'EF15886EFE80FB8610FBACC59CA9AB57EEFA8F703C08551B676FC2A07136FA28')

makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_archive}"
  apply_patch_with_msg \
    0001-define-macro-NGETRUSAGE.patch
}

build() {
  cd "${srcdir}"
  rm -rf python{2,3}-build
  for builddir in python{2,3}-build; do
    msg2 "Building for ${CARCH} ${builddir%-build} ..."
    cp -r ${_archive} ${builddir}
    pushd $builddir
    ${MINGW_PREFIX}/bin/${builddir%-build} setup.py --quiet build
    popd
  done
}

package_python3-pycosat() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}" "${_deps3[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-pycosat() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}" "${_deps2[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-pycosat() {
  package_python2-pycosat
}

package_mingw-w64-i686-python3-pycosat() {
  package_python3-pycosat
}

package_mingw-w64-x86_64-python2-pycosat() {
  package_python2-pycosat
}

package_mingw-w64-x86_64-python3-pycosat() {
  package_python3-pycosat
}
