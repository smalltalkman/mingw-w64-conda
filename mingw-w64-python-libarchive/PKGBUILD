# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=libarchive
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.4.6
pkgrel=1
pkgdesc="A very intuitive and useful adapter to libarchive for universal archive access (mingw-w64)"
arch=('any')
url="https://github.com/dsoprea/PyEasyArchive"
license=('GPL-2.0')
_dtoken="b8/89/2952eefad82d9361f539de16ea85a2d98a7fb68eae2ba9ab0869aa2cee0f"
_archive=${_realname}-${pkgver}
source=(${_archive}.tar.gz::https://files.pythonhosted.org/packages/${_dtoken}/${_archive}.tar.gz
        0001-fix-setup.py.patch)
sha256sums=('37E8CCA1EB85D30583CDCFFC58116D83ABC09BE7549D5D6C9EAD563C0A8D7B04'
            'F0D61AE16E096FEBAACFB68C5CBA7DA846D7B7CC1D7CE9AE69DCC2603D2A4F14')

_deps=('nose')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  rm -rf "${srcdir}/${_archive}.orig"
  cp -rp "${srcdir}/${_archive}" "${srcdir}/${_archive}.orig"

  cd "${srcdir}/${_archive}"
  apply_patch_with_msg \
    0001-fix-setup.py.patch
}

build() {
  cd "${srcdir}"
  rm -rf python{2,3}-build
  for builddir in python{2,3}-build; do
    msg2 "Building for ${CARCH} ${builddir%-build} ..."
    cp -r ${_archive} ${builddir}
    pushd $builddir
    ${MINGW_PREFIX}/bin/${builddir%-build} setup.py --quiet build
    popd
  done
}

package_python3-libarchive() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}" "${_deps3[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}")

  local _mingw_prefix=$(cygpath -am ${MINGW_PREFIX})

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-libarchive() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}" "${_deps2[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-libarchive() {
  package_python2-libarchive
}

package_mingw-w64-i686-python3-libarchive() {
  package_python3-libarchive
}

package_mingw-w64-x86_64-python2-libarchive() {
  package_python2-libarchive
}

package_mingw-w64-x86_64-python3-libarchive() {
  package_python3-libarchive
}
