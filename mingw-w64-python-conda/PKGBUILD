# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=conda
pkgbase=mingw-w64-python-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python-${_realname}"
pkgver=4.13.0
pkgrel=1
pkgdesc="OS-agnostic, system-level binary package manager and ecosystem (mingw-w64)"
arch=('any')
url="https://github.com/conda/conda"
license=('BSD-3-Clause')
mingw_arch=('mingw32' 'mingw64')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-pycosat"
         "${MINGW_PACKAGE_PREFIX}-python-requests"
         "${MINGW_PACKAGE_PREFIX}-python-menuinst"
         "${MINGW_PACKAGE_PREFIX}-python-ruamel-yaml")
makedepends=("patch"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-nose")
install=${_realname}-${MINGW_ARCH}.install
source=(${_realname}-${pkgver}.tar.gz::https://github.com/conda/conda/archive/${pkgver}.tar.gz
        0001-msys2-mingw.patch)
sha256sums=('30c4cee48ef3acc4e2d802c05baee44f5385f55b0c23c547b0961c51e1d140d2'
            '47d082c831f13c9e1cc8172d1ec4b3ab6ec221eb6c1c2491371c28c0cc3310a4')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  rm -rf "${srcdir}/${_realname}-${pkgver}.orig"
  cp -rp "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}.orig"

  cd "${srcdir}/${_realname}-${pkgver}"
  echo ${pkgver} > conda/.version

  apply_patch_with_msg \
    0001-msys2-mingw.patch

  rm -rf "${srcdir}/${_realname}-${pkgver}.ptch"
  cp -rp "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}.ptch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python setup.py --quiet build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  local python_platform=$(python -c 'import sysconfig; print(sysconfig.get_platform())')
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  local python_path=$(cygpath -m "$PWD/build/lib.${python_platform}-${python_version}")
  PYTHONPATH="$python_path;$PYTHONPATH" \
  ${MINGW_PREFIX}/bin/nosetests || warning "Tests failed"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"

  install -dm755 "${pkgdir}${MINGW_PREFIX}"/condabin/
  install -dm755 "${pkgdir}${MINGW_PREFIX}"/shell/condabin/
  install -dm755 "${pkgdir}${MINGW_PREFIX}"/etc/profile.d/
  install -dm755 "${pkgdir}${MINGW_PREFIX}"/etc/fish/conf.d/
  install -dm755 "${pkgdir}${MINGW_PREFIX}"/lib/python3.9/site-packages/xontrib/
}
