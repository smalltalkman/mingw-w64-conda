# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=menuinst
pkgbase=mingw-w64-python-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python-${_realname}"
pkgver=1.4.16
pkgrel=1
pkgdesc="Cross platform menu item installation (mingw-w64)"
arch=('any')
url="https://github.com/conda/menuinst"
license=('BSD 3-clause')
mingw_arch=('mingw32' 'mingw64')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-pywin32")
makedepends=("patch"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/conda/menuinst/archive/${pkgver}.tar.gz
        0001-fix-initwinshortcut-function-prototype.patch
        0002-fix-setup.py.patch)
sha256sums=('c9ad6e225c2831656bac35b71ac1c8c3f82f08ba8da9718dfb3708485bbccc44'
            'a8421660bce42498accc876daa1ce051028955ee10567beb5abc7edb54cd3ca1'
            'b34c3e43287edfc61de4c018bcc84d1cb3d3c1b8a108709c97228a54a933f515')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  rm -rf "${srcdir}/${_realname}-${pkgver}.orig"
  cp -rp "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}.orig"

  cd "${srcdir}/${_realname}-${pkgver}"
  apply_patch_with_msg \
    0001-fix-initwinshortcut-function-prototype.patch \
    0002-fix-setup.py.patch

  rm -rf "${srcdir}/${_realname}-${pkgver}.ptch"
  cp -rp "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}.ptch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python setup.py --quiet build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
}
