# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=menuinst
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.4.16
pkgrel=1
pkgdesc="Cross platform menu item installation (mingw-w64)"
arch=('any')
url="https://github.com/ContinuumIO/menuinst"
license=('BSD 3-clause')
_archive=${_realname}-${pkgver}
source=(${_archive}.tar.gz::https://github.com/ContinuumIO/menuinst/archive/${pkgver}.tar.gz
        0001-fix-initwinshortcut-function-prototype.patch)
sha256sums=('C9AD6E225C2831656BAC35B71AC1C8C3F82F08BA8DA9718DFB3708485BBCCC44'
            'A8421660BCE42498ACCC876DAA1CE051028955EE10567BEB5ABC7EDB54CD3CA1')

makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_archive}"
  apply_patch_with_msg \
    0001-fix-initwinshortcut-function-prototype.patch
}

build() {
  cd "${srcdir}"
  rm -rf python{2,3}-build
  for builddir in python{2,3}-build; do
    msg2 "Building for ${CARCH} ${builddir%-build} ..."
    cp -r ${_archive} ${builddir}
    pushd $builddir
    ${MINGW_PREFIX}/bin/${builddir%-build} setup.py --quiet build
    popd
  done
}

package_python3-menuinst() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}" "${_deps3[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-menuinst() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}" "${_deps2[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-menuinst() {
  package_python2-menuinst
}

package_mingw-w64-i686-python3-menuinst() {
  package_python3-menuinst
}

package_mingw-w64-x86_64-python2-menuinst() {
  package_python2-menuinst
}

package_mingw-w64-x86_64-python3-menuinst() {
  package_python3-menuinst
}
