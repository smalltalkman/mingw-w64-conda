# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=ruamel.ordereddict
pkgbase=mingw-w64-python2-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}")
pkgver=0.4.14
pkgrel=1
pkgdesc="Implementation of an ordered dictionary with Key Insertion Order, Key Value Insertion Order (mingw-w64)"
arch=('any')
url="https://bitbucket.org/ruamel/ordereddict"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python2")
makedepends=("patch"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
_dtoken="7d/fd/522c4722347aaf01e6bc8cce486130d83c52c72d3c3c2bbe3cbb02dddd4d"
source=(${_realname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/${_dtoken}/${_realname}-${pkgver}.tar.gz
        0001-change-_MSC_VER-to-_WIN32.patch)
sha256sums=('281051d26eb2b18ef3d920e1e260716a52bd058a6b1a2f324102fc6a15cb8d4a'
            'f79e55350f3fec0c9401691c2bde80f84a88e620531ecfd3fcfe00c35bc68e69')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -b -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  apply_patch_with_msg \
    0001-change-_MSC_VER-to-_WIN32.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ${MINGW_PREFIX}/bin/python2 setup.py --quiet build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1
}
