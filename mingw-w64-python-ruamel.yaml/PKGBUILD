# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=ruamel.yaml
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.15.100
pkgrel=1
pkgdesc="YAML 1.2 loader/dumper package for Python (mingw-w64)"
arch=('any')
url="https://bitbucket.org/ruamel/yaml"
license=('MIT')
_dtoken="9a/ee/55cd64bbff971c181e2d9e1c13aba9a27fd4cd2bee545dbe90c44427c757"
_archive=${_realname}-${pkgver}
source=(${_archive}.tar.gz::https://files.pythonhosted.org/packages/${_dtoken}/${_archive}.tar.gz)
sha256sums=('8e42f3067a59e819935a2926e247170ed93c8f0b2ab64526f888e026854db2e4')

_deps2=('ruamel.ordereddict')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")

build() {
  cd "${srcdir}"
  rm -rf python{2,3}-build
  for builddir in python{2,3}-build; do
    msg2 "Building for ${CARCH} ${builddir%-build} ..."
    cp -r ${_archive} ${builddir}
    pushd $builddir
    ${MINGW_PREFIX}/bin/${builddir%-build} setup.py --quiet build
    popd
  done
}

package_python3-ruamel.yaml() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}" "${_deps3[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}")

  cd "${srcdir}/python3-build"
  RUAMEL_NO_PIP_INSTALL_CHECK=1 \
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-ruamel.yaml() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}" "${_deps2[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}")

  cd "${srcdir}/python2-build"
  RUAMEL_NO_PIP_INSTALL_CHECK=1 \
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-ruamel.yaml() {
  package_python2-ruamel.yaml
}

package_mingw-w64-i686-python3-ruamel.yaml() {
  package_python3-ruamel.yaml
}

package_mingw-w64-x86_64-python2-ruamel.yaml() {
  package_python2-ruamel.yaml
}

package_mingw-w64-x86_64-python3-ruamel.yaml() {
  package_python3-ruamel.yaml
}
