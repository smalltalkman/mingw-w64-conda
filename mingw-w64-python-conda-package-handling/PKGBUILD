# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=conda-package-handling
pkgbase=mingw-w64-python-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python-${_realname}"
pkgver=1.8.1
pkgrel=1
pkgdesc="Create and extract conda packages of various formats (mingw-w64)"
arch=('any')
url="https://github.com/conda/conda-package-handling"
license=('BSD-3-Clause')
mingw_arch=('mingw32' 'mingw64')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-six")
makedepends=("patch"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-libarchive")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-nose")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/conda/conda-package-handling/archive/${pkgver}.tar.gz
        0001-fix-setup.py.patch)
sha256sums=('22d89206f55616b774a775038a6777b0d2e9b8706c23068c4d3091f114c8a4e8'
            '203e75fb802966528b6cafce790ef7c45363398819f2dd48fbea7e74af8748c5')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  rm -rf "${srcdir}/${_realname}-${pkgver}.orig"
  cp -rp "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}.orig"

  cd "${srcdir}/${_realname}-${pkgver}"
  apply_patch_with_msg \
    0001-fix-setup.py.patch

  rm -rf "${srcdir}/${_realname}-${pkgver}.ptch"
  cp -rp "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}.ptch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python setup.py --quiet build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  local python_platform=$(python -c 'import sysconfig; print(sysconfig.get_platform())')
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  local python_path=$(cygpath -m "$PWD/build/lib.${python_platform}-${python_version}")
  PYTHONPATH="$python_path;$PYTHONPATH" \
  ${MINGW_PREFIX}/bin/nosetests || warning "Tests failed"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
